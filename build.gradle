group 'kondorcet'
version '1.0.0'

buildscript {
    ext.api_version = '1.0'

    ext.kotlin_version = '1.1.1'
    ext.dokka_version = '0.9.13'
    ext.gradle_git_version = '1.7.0'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "org.ajoberstar:gradle-git:$gradle_git_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'com.github.slimaku:kable:v1.1.0'

    testCompile 'junit:junit:4.12'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

dokka {
    includes = ['Packages.md']
}

task sourcesJar(type: Jar) {
    from sourceSets.main.kotlin.srcDirs
    classifier = 'sources'
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "$buildDir/dokkaJavadoc"
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

githubPages {
    pages {
        from dokka
        into "doc/${api_version}"
    }
}

check.dependsOn jacocoTestReport
