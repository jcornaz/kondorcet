group 'kondorcet'
version '1.0-SNAPSHOT'

buildscript {
    ext.api_version = '1.0'

    ext.kotlin_version = '1.0.4'
    ext.dokka_version = '0.9.9'
    ext.gradle_git_version = '1.5.1'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "org.ajoberstar:gradle-git:$gradle_git_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'jacoco'

sourceCompatibility = 1.6

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.github.slimaku:kraft:v0.1.2'

    testCompile 'junit:junit:4.12'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task kotlinCoverageReport(dependsOn: 'jacocoTestReport') << {
    def reports = new File(project.buildDir, '/reports/jacoco/test/html')
    def index = new File(reports, 'index.html').getText('UTF-8')
    def footer = '<div class="footer">'
    def result = new StringBuilder(index.substring(0, index.indexOf(footer)))

    reports.eachDirMatch({ !new File(it).name.startsWith(".") }) { dir ->
        def moduleIndex = new File(dir, 'index.source.html')
        def content = moduleIndex.getText('UTF-8')
                .replaceAll('\\.\\./\\.resources', '.resources')
                .replaceAll('href="', "href=\"${dir.name}/")
        result.append(content.substring(content.indexOf('<h1>'), content.indexOf(footer)))
    }
    result.append(index.substring(index.indexOf(footer)))

    def output = new File(reports, 'index.kotlin.html')

    output.delete()
    output << result.toString()
    println("open $output.path")
}

githubPages {
    pages {
        from dokka
        into "doc/${api_version}"
    }
}

check.dependsOn jacocoTestReport
